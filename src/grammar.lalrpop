use std::str::FromStr;
use ast::{Expr, BinaryOpCode, UnaryOpCode};

grammar;

pub Exprs = Comma<Expr>;

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Tier<Op,NextTier>: Box<Expr> = {
    Tier<Op,NextTier> Op NextTier => Box::new(Expr::BinaryOp(<>)),
    NextTier
};

UnaryTier<Op, NextTier>: Box<Expr> = {
    Op UnaryTier<Op, NextTier> => Box::new(Expr::UnaryOp(<>)),
    NextTier,
};

pub Expr = OrTest;
OrTest = Tier<OrOp, AndTest>;
AndTest = Tier<AndOp, NotTest>;
NotTest = UnaryTier<BoolNotOp, ComparisonExpr>;
ComparisonExpr = {
    AExpr ComparisonOp AExpr => Box::new(Expr::BinaryOp(<>)),
    AExpr,
};
AExpr = Tier<AOp, MExpr>;
MExpr = Tier<MOp, Term>;

ComparisonOp: BinaryOpCode = {
    "<" => BinaryOpCode::LessThan,
    "<=" => BinaryOpCode::LessThanOrEqual,
    ">" => BinaryOpCode::MoreThan,
    ">=" => BinaryOpCode::MoreThanOrEqual,
    "==" => BinaryOpCode::Equal,
    "!=" => BinaryOpCode::NotEqual,
};

BoolNotOp: UnaryOpCode = {
    "not" => UnaryOpCode::BoolNot,
};

OrOp: BinaryOpCode = {
    "or" => BinaryOpCode::BoolOr,
};

AndOp: BinaryOpCode = {
    "and" => BinaryOpCode::BoolAnd,
};

AOp: BinaryOpCode = {
    "+" => BinaryOpCode::Add,
    "-" => BinaryOpCode::Sub,
};

MOp: BinaryOpCode = {
    "*" => BinaryOpCode::Mul,
    "/" => BinaryOpCode::Div,
};

Term: Box<Expr> = {
    IntegerLiteral => Box::new(Expr::Number(<>)),
    "(" <Expr> ")"
};

IntegerLiteral: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};
